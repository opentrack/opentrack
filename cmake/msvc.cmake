if(CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT OR "${CMAKE_INSTALL_PREFIX}" STREQUAL "")
    set(CMAKE_INSTALL_PREFIX "${CMAKE_BINARY_DIR}/install" CACHE PATH "" FORCE)
endif()

if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/opentrack-policy.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/opentrack-policy.cmake" NO_POLICY_SCOPE)
endif()

# search for programs in the host directories
SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
# don't poison with system compile-time data
SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)

#add_compile_options(-Qvec-report:2)
#add_compile_options(-d2cgsummary)
add_definitions(-D_HAS_EXCEPTIONS=0)

if(DEFINED CMAKE_TOOLCHAIN_FILE)
    # ignore cmake warning: Manually-specified variable not used by the project
    set(CMAKE_TOOLCHAIN_FILE "${CMAKE_TOOLCHAIN_FILE}")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/opentrack-policy.cmake" NO_POLICY_SCOPE)
set(CMAKE_POLICY_DEFAULT_CMP0069 NEW CACHE INTERNAL "" FORCE)
#set(CMAKE_INTERPROCEDURAL_OPTIMIZATION ON)

set(CMAKE_C_EXTENSIONS FALSE)
set(CMAKE_CXX_EXTENSIONS FALSE)

function(sets type)
    list(LENGTH ARGN len)
    math(EXPR rem "${len} % 2")
    if(rem)
        message(FATAL_ERROR "argument count not even")
    endif()
    while(NOT ARGN STREQUAL "")
        list(POP_FRONT ARGN name value)
        set(${name} "${value}" CACHE "${type}" "" FORCE)
    endwhile()
endfunction()

if(CMAKE_PROJECT_NAME STREQUAL "opentrack")
    #include("${CMAKE_CURRENT_LIST_DIR}/opentrack-policy.cmake" NO_POLICY_SCOPE)

    add_compile_options("-W4")

    # C4265: class has virtual functions, but destructor is not virtual
    # C4005: macro redefinition
    set(warns-error 4265 4005)

    foreach(i ${warns-error})
        add_compile_options(-w1${i} -we${i})
    endforeach()

    set(opentrack-use-onnxruntime-avx-dispatch 1)

elseif(CMAKE_PROJECT_NAME STREQUAL "OpenCV")
    set(PARALLEL_ENABLE_PLUGINS FALSE)
    set(HIGHGUI_ENABLE_PLUGINS FALSE)
    set(VIDEOIO_ENABLE_PLUGINS FALSE)

    set(ENABLE_FAST_MATH                        ON)
    set(ENABLE_PRECOMPILED_HEADERS              ON)
    set(CV_TRACE                                OFF)
    set(OPENCV_DISABLE_THREAD_SUPPORT           1)
    set(OPENCV_SKIP_MSVC_EXCEPTIONS_FLAG        1)
    set(OPENCV_SKIP_MSVC_PARALLEL               1)
    set(BUILD_PACKAGE                           OFF)
    set(BUILD_PERF_TESTS                        OFF)
    set(BUILD_PROTOBUF                          OFF)
    set(BUILD_TESTS                             OFF)
    set(BUILD_PERF_TESTS                        OFF)
    set(BUILD_opencv_apps                       OFF)
    set(BUILD_opencv_dnn                        OFF)
    set(BUILD_opencv_gapi                       OFF)
    set(BUILD_opencv_java_bindings_generator    OFF)
    set(BUILD_opencv_js                         OFF)
    set(BUILD_opencv_js_bindings_generator      OFF)
    set(BUILD_opencv_ml                         OFF)
    set(BUILD_opencv_objc_bindings_generator    OFF)
    set(BUILD_opencv_photo                      OFF)
    set(BUILD_opencv_python_bindings_generator  OFF)
    set(BUILD_opencv_python_tests               OFF)
    set(BUILD_opencv_stitching                  OFF)
    set(BUILD_opencv_ts                         OFF)

elseif(CMAKE_PROJECT_NAME STREQUAL "TestOscpack")
    add_compile_definitions(OSC_HOST_LITTLE_ENDIAN)

elseif(PROJECT_NAME STREQUAL "onnxruntime")
    set(onnxruntime_BUILD_SHARED_LIB           ON)
    set(BUILD_SHARED_LIBS                      OFF)
    set(ONNX_USE_MSVC_STATIC_RUNTIME           OFF)
    set(protobuf_MSVC_STATIC_RUNTIME           OFF)
    set(ABSL_MSVC_STATIC_RUNTIME               OFF)
    set(BUILD_TESTING                          OFF)
    set(onnxruntime_BUILD_BENCHMARKS           OFF)
    set(onnxruntime_BUILD_FOR_NATIVE_MACHINE   OFF)
    set(onnxruntime_BUILD_UNIT_TESTS           OFF)
    set(protobuf_BUILD_EXAMPLES                OFF)
    set(protobuf_BUILD_SHARED_LIBS             OFF)
    set(ONNX_BUILD_BENCHMARKS                  OFF)
    set(ONNX_BUILD_TESTS                       OFF)
    set(ONNX_DISABLE_EXCEPTIONS                OFF)
    set(ONNX_GEN_PB_TYPE_STUBS                 OFF)
    set(onnxruntime_DISABLE_CONTRIB_OPS        ON)

elseif(PROJECT_NAME STREQUAL "Qt")
    set(QT_BUILD_SUBMODULES "qtbase;qtserialport;qttools")
    set(QT_BUILD_DOCS           OFF)
    set(BUILD_qtactiveqt        OFF)
    set(BUILD_qtdeclarative     OFF)
    set(BUILD_qtimageformats    OFF)
    set(BUILD_qtlanguageserver  OFF)
    set(BUILD_qtshadertools     OFF)
    set(BUILD_qtsvg             OFF)
    set(FEATURE_qdoc OFF)
    set(FEATURE_debug OFF)
    set(FEATURE_debug_and_release OFF)
    set(FEATURE_force_debug_info ON)
    set(FEATURE_shared ON)
    set(FEATURE_ltcg ON)

    add_definitions(-DNDEBUG)

    get_filename_component(_pfx "${CMAKE_SOURCE_DIR}" DIRECTORY)
    set(_proj "qt-${PROJECT_VERSION}-msvc-${MSVC_YEAR}")
    message(FATAL_ERROR "year: '${MSVC_YEAR}'")
    set(CMAKE_INSTALL_PREFIX "${_pfx}/${_proj}" CACHE PATH "" FORCE)
endif()

set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreaded$<$<CONFIG:Debug>:Debug>DLL")
#add_compile_options(-MD)

set(_CFLAGS "-diagnostics:caret -Zc:inline -Zc:static_assert- -wd4117 -Zi -Zf -Zo -bigobj -cgthreads1 -vd0")
set(_CFLAGS_RELEASE "-O2 -Oit -Oy- -Ob3 -fp:fast -GS- -GF -GL -Gw -Gy")
set(_CFLAGS_DEBUG "-guard:cf -MTd -Gs0 -RTCs")

set(_CXXFLAGS "${_CFLAGS} -Zc:throwingNew -Zc:lambda")
set(_CXXFLAGS_RELEASE "${_CFLAGS_RELEASE}")
set(_CXXFLAGS_DEBUG "${_CFLAGS_DEBUG}")

set(_LDFLAGS "")
set(_LDFLAGS_RELEASE "-OPT:REF,ICF=10 -DEBUG:FULL")
set(_LDFLAGS_DEBUG "-DEBUG:FULL")

set(_LDFLAGS_STATIC "")
set(_LDFLAGS_STATIC_RELEASE "")
set(_LDFLAGS_STATIC_DEBUG "")

set(CMAKE_BUILD_TYPE Release)

string(TOUPPER "${CMAKE_BUILD_TYPE}" CMAKE_BUILD_TYPE)
set(CMAKE_BUILD_TYPE "${CMAKE_BUILD_TYPE}" CACHE STRING "" FORCE)
if (NOT CMAKE_BUILD_TYPE STREQUAL "RELEASE" AND NOT CMAKE_BUILD_TYPE STREQUAL "DEBUG")
    set(CMAKE_BUILD_TYPE "RELEASE" CACHE STRING "" FORCE)
endif()

foreach(k "" "_${CMAKE_BUILD_TYPE}")
    set("FLAGS_CXX${k}"     "" CACHE STRING "More CMAKE_CXX_FLAGS${k}")
    set("FLAGS_C${k}"       "" CACHE STRING "More CMAKE_C_FLAGS${k} (almost never used)")
    set("FLAGS_LD${k}"      "" CACHE STRING "More CMAKE_(SHARED|EXE|MODULE)_LINKER_FLAGS${k}")
    set("FLAGS_ARCHIVE${k}" "" CACHE STRING "More CMAKE_STATIC_LINKER_FLAGS${k}")
endforeach()

foreach(k "" _DEBUG _RELEASE)
    #set(CMAKE_STATIC_LINKER_FLAGS${k} "${CMAKE_STATIC_LINKER_FLAGS${k}} ${_LDFLAGS_STATIC${k}}")
    set(CMAKE_STATIC_LINKER_FLAGS${k} "${_LDFLAGS_STATIC${k}} ${FLAGS_ARCHIVE${k}}" CACHE STRING "" FORCE)
endforeach()
foreach(j "" _DEBUG _RELEASE)
    foreach(i MODULE EXE SHARED)
        #set(CMAKE_${i}_LINKER_FLAGS${j} "${CMAKE_${i}_LINKER_FLAGS${j}} ${_LDFLAGS${j}}")
        set(CMAKE_${i}_LINKER_FLAGS${j} "${_LDFLAGS${j}} ${FLAGS_LD${j}}" CACHE STRING "" FORCE)
    endforeach()
endforeach()

foreach(j C CXX)
    foreach(i "" _DEBUG _RELEASE)
        #set(CMAKE_${j}_FLAGS${i} "${CMAKE_${j}_FLAGS${i}} ${_${j}FLAGS${i}}")
        set(CMAKE_${j}_FLAGS${i} "${_${j}FLAGS${i}} ${FLAGS_${j}${i}}" CACHE STRING "" FORCE)
    endforeach()
endforeach()
