include_guard(GLOBAL)
if(CMAKE_HOST_SYSTEM_NAME STREQUAL "Windows")
    set(__sdk_username "$ENV{USERNAME}")
else()
    set(__sdk_username "$ENV{USER}")
endif()

if(".${__sdk_username}" STREQUAL ".")
    set(__sdk_username "(I-have-no-name)")
endif()

if(WIN32)
    set(__sdk_target_os "windows")
else()
    string(REGEX MATCH "^Linux (.+)-Microsoft\$" __sdk_target_os_match "${CMAKE_SYSTEM_NAME} ${CMAKE_SYSTEM_VERSION}")
    if(__sdk_target_os_match)
        set(__sdk_target_os "Windows-WSL")
    else()
        set(__sdk_target_os "${CMAKE_SYSTEM_NAME}")
    endif()
endif()

if(NOT CMAKE_SYSTEM_NAME STREQUAL CMAKE_HOST_SYSTEM_NAME)
    set(__sdk_host_os "${CMAKE_HOST_SYSTEM_NAME}-cross-")
else()
    set(__sdk_host_os "")
endif()

if(DEFINED OPENTRACK_USERCONFIG)
    if(IS_ABSOLUTE OPENTRACK_USERCONFIG)
        set(__sdk_file "${OPENTRACK_USERCONFIG}")
    else()
        set(__sdk_file "${CMAKE_CURRENT_SOURCE_DIR}/${OPENTRACK_USERCONFIG}")
    endif()
    if (NOT EXISTS "${__sdk_file}")
        message(FATAL_ERROR "can't find userconfig '${__sdk_file}'")
    endif()
else()
    set(__sdk_uh "${__sdk_username}@${CMAKE_CXX_COMPILER_ID}-${__sdk_host_os}${__sdk_target_os}")
    set(__sdk_file "${CMAKE_CURRENT_SOURCE_DIR}/sdk-paths-${__sdk_uh}.cmake")
endif()

if(EXISTS "${__sdk_file}")
    message(STATUS "Loading user settings '${__sdk_file}'")
    include("${__sdk_file}")
else()
    message(STATUS "User settings file '${__sdk_file}' doesn't exist")
endif()
