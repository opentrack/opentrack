add_compile_definitions(PS3EYE_DEBUG)

if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/ps3eye-driver/CMakeLists.txt")
    add_subdirectory("ps3eye-driver")

    if(NOT MSVC)
        if(PKG_CONFIG_FOUND)
            pkg_check_modules(libusb "libusb-1.0" QUIET IMPORTED_TARGET)
        endif()
        if(libusb_FOUND)
            include_directories(SYSTEM ${libusb_INCLUDE_DIRS})
            link_libraries(${libusb_LIBRARIES})
            link_directories(${libusb_LIBRARY_DIRS})
            set(_bins "")
            foreach(_dir IN LISTS libusb_LIBRARY_DIRS)
                list(APPEND _bins "${_dir}/../bin")
            endforeach()
            find_file(LIBUSB_DLL NAMES "libusb-1.0.dll" HINTS ${_bins} NO_DEFAULT_PATH)
            if(LIBUSB_DLL)
                install(FILES "${LIBUSB_DLL}"
                        DESTINATION "${opentrack-libexec}"
                        PERMISSIONS ${opentrack-perms-exec})
            else()
                otr_install_lib(PkgConfig::libusb "${opentrack-libexec}")
            endif()
        endif()
    else()
        set(SDK_LIBUSB CACHE PATH "")
        if(SDK_LIBUSB)
            set(libusb_FOUND TRUE)
            link_directories("${SDK_LIBUSB}")
            include_directories(SYSTEM "${SDK_LIBUSB}")
        endif()
    endif()
endif()

if(TARGET ps3eye-sdl AND FALSE)
    install(TARGETS "ps3eye-sdl" DESTINATION "${opentrack-libexec}")
    if(WIN32)
        foreach(k ${SDL2_LIBRARIES})
            get_filename_component(path "${k}" PATH)
            set(lib "${path}/SDL2.dll")
            if(EXISTS "${lib}")
                otr_install_lib("${lib}" "${opentrack-libexec}")
                break()
            endif()
        endforeach()
    endif()
endif()

if(TARGET ps3eye-mode-test)
    install(TARGETS "ps3eye-mode-test" DESTINATION "${opentrack-libexec}")
endif()

if(TARGET ps3eye-driver)
    add_executable(ps3eye-subprocess "wrapper.cxx" "shm.cxx")
    target_link_libraries(ps3eye-subprocess ps3eye-driver)
    if(CMAKE_HOST_SYSTEM_NAME STREQUAL "Linux")
        target_link_libraries(ps3eye-subprocess rt)
    endif()
    install(TARGETS "ps3eye-subprocess" DESTINATION "${opentrack-libexec}")
endif()

if(TARGET ps3eye-subprocess)
    otr_module(video-ps3eye)
    target_link_libraries(${self} PRIVATE ps3eye-driver opentrack-video)
    if(WIN32)
        set(path "${SDK_LIBUSB}/libusb-1.0.dll")
        if(EXISTS "${path}")
            otr_install_lib("${path}" "${opentrack-libexec}")
        endif()
        set(vcrun "${SDK_LIBUSB}/vcruntime140.dll")
        if(EXISTS "${vcrun}")
            otr_install_lib("${vcrun}" "${opentrack-libexec}")
        endif()
    endif()
endif()

if(TARGET ps3eye-frame-test)
    install(TARGETS "ps3eye-frame-test" DESTINATION "${opentrack-libexec}")
endif()
